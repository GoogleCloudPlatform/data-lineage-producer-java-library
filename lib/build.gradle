// Copyright 2024 Google LLC
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
// https://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

plugins {
    id 'java-library'
//    TODO upgrade this to https://plugins.gradle.org/plugin/com.gradleup.shadow
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'com.github.sherter.google-java-format' version '0.9'
    id 'checkstyle'
    id 'maven-publish'
    id 'signing'

// v"2.2.5" is recommended, but it uses Java21 which is incompatible with other
// plugins (e.g. "com.github.johnrengelman.shadow"), therefore, using latest compatible version
    id("com.google.cloud.artifactregistry.gradle-plugin") version "2.1.5"
}

googleJavaFormat {
    toolVersion = '1.7'
}

ext {
    cloudBomVersion = '26.37.0'
    protobufVersion = '3.23.0'
    grpcProtobufVersion = '1.54.1'
    gaxVersion = '2.29.0'
    gaxHttpJsonVersion = '0.103.7'
    gaxTestLibVersion = '2.12.2'
    guavaVersion = '33.2.1-jre'
    junitVersion = '4.13.2'
    truthVersion = '1.1.3'
    mockitoVersion = '3.+'
    parameterInjectorVersion = '1.8'
    slf4jVersion = '2.0.9'
    logbackVersion = '1.4.14'
    lombokVersion = '1.18.30'
}

repositories {
    // Use Maven Central for resolving dependencies.
    mavenCentral()
}

dependencies {
    implementation platform("com.google.cloud:libraries-bom:${cloudBomVersion}")
    implementation "com.google.cloud:google-cloud-datalineage:0.60.0"

    implementation "com.google.protobuf:protobuf-java-util:${protobufVersion}"
    implementation "com.google.api:gax:${gaxVersion}"
    implementation "com.google.api:gax-grpc:${gaxVersion}"
    implementation "com.google.api:gax-httpjson:${gaxHttpJsonVersion}"
    implementation "io.grpc:grpc-protobuf:${grpcProtobufVersion}"
    implementation "com.google.guava:guava:${guavaVersion}"

    // SLF4J logging
    implementation "org.slf4j:slf4j-api:${slf4jVersion}"

    // Lombok for @Slf4j annotation
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    // AutoValue
    compileOnly "com.google.auto.value:auto-value-annotations:1.10.1"
    annotationProcessor "com.google.auto.value:auto-value:1.10.1"

    // Use JUnit test framework.
    testImplementation "junit:junit:${junitVersion}"
    testImplementation "com.google.truth:truth:${truthVersion}"
    testImplementation "org.mockito:mockito-core:${mockitoVersion}"
    testImplementation "com.google.api:gax:${gaxTestLibVersion}:testlib"
    testImplementation "com.google.testparameterinjector:test-parameter-injector:${parameterInjectorVersion}"

    // Test logging dependencies
    testImplementation "ch.qos.logback:logback-classic:${logbackVersion}"
    testImplementation "ch.qos.logback:logback-core:${logbackVersion}"

    // Test Lombok
    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
}

test {
    include '**/*Test.class'
}

shadowJar {
    archiveBaseName.set('data-lineage-producer-java-library')
    archiveClassifier.set('')

    def excludedPrefixes = [
            'android/', 'javax/', 'io/opentelemetry', 'com/google/', 'opencensus/', 'grpc/', 'google/', 'org/'
    ]

    def includedPrefixes = [
            'com/google/api', 'com/google/auth', 'com/google/common',
            'com/google/gson', 'com/google/protobuf', 'com/google/longrunning',
            'com/google/rpc', 'com/google/cloud/datalineage',
            'com/google/cloud/datacatalog', 'org/threeten'
    ]

    def includedExactPaths = [
            'com/google/cloud', 'org'
    ]

    // exclude from shaded jar all packages starting with excludedPrefixes
    // but keep those starting with includedPrefixes or containing exact paths from includedExactPaths
    exclude { FileTreeElement fte ->
        def isExcluded = excludedPrefixes.any { fte.path.startsWith(it) }
        def isIncluded = includedPrefixes.any { fte.path.startsWith(it) }
                || includedExactPaths.contains(fte.path)
        return isExcluded && !isIncluded
    }

    [
            'com.google.guava',
            'com.google.longrunning',
            'com.google.rpc',
            'io.opencensus',
            'io.perfmark',
            'com.google.common',
            'com.google.gson',
            'org.threeten.bp'
    ].each { packageName ->
        relocate packageName, "datalineage.shaded.${packageName}"
    }

    mergeServiceFiles()
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            version = '1.1.0'
            group = 'com.google.cloud.datalineage'
            artifactId = 'producerclient'

            artifact shadowJar
            artifact javadocJar
            artifact sourcesJar

            pom {
                name = 'Java producer library for Data Lineage.'
                description = 'Library that provides synchronous and asynchronous clients for interacting with the Google Cloud Data Lineage API.'
                url = 'https://github.com/GoogleCloudPlatform/data-lineage-producer-java-library'
                properties = [:]
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'midamkina'
                        name = 'Mary Idamkina'
                        email = 'midamkina@google.com'
                    }
                    developer {
                        id = 'gabrysiaolsz'
                        name = 'Gabriela Olszewska'
                        email = 'gabrysiaolsz@google.com'
                    }
                    developer {
                        id = 'ngorchakova'
                        name = 'Natalia Gorchakova'
                        email = 'ngorchakova@google.com'
                    }
                    developer {
                        id = 'mikhalevich'
                        name = 'Konstantin Mikhalevich'
                        email = 'mikhalevich@google.com'
                    }
                    developer {
                        id = 'wojtekl'
                        name = 'Wojciech ≈Åowiec'
                        email = 'wojtekl@google.com'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/GoogleCloudPlatform/data-lineage-producer-java-library.git'
                    developerConnection = 'scm:git:ssh://git@github.com/GoogleCloudPlatform/data-lineage-producer-java-library.git'
                    url = 'https://github.com/GoogleCloudPlatform/data-lineage-producer-java-library'
                }
            }
        }
    }
    repositories {
        maven {
            url "artifactregistry://us-central1-maven.pkg.dev/dataplex-gob-louhi-releaser/cloud-dataplex-maven-releases"
        }
    }
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
